module Tier3;
use Structures: all;
export all;

specialize int[.,.] relax(int[3,3] weights, int[.,.] arr);
specialize int[.,.] relax(int[5,5] weights, int[.,.] arr);

inline
int[2:shp] relax(int[m,m] weights, int[2:shp] arr)
{
    return { iv -> sum({ ov -> weights[ov] * rotate(m / 2 - ov, arr)[iv] })
           | iv < shp };
}

inline
int[2:shp] gaussBlur(int[2:shp] img)
{
    weights = [
        [1, 2, 1],
        [2, 3, 2],
        [1, 2, 1]
    ];
    return relax(weights, img) / sum(weights);
}

inline
color[2:shp] gaussBlur(color[2:shp] img)
{
    int_img = toi(img);
    int_res = { [.,.,i] -> gaussBlur(int_img[.,.,i]) };
    return { iv -> newColor( int_res[iv]) | iv < shp };
}

inline
int[2:shp] gaussBlur25(int[2:shp] img)
{
    weights = [
        [1,  4,  7,  4, 1],
        [4, 20, 33, 20, 4],
        [7, 33, 55, 33, 7],
        [4, 20, 33, 20, 4],
        [1,  4,  7,  4, 1]
    ];
    return relax(weights, img) / sum(weights);
}

inline
color[2:shp] gaussBlur25(color[2:shp] img)
{
    int_img = toi(img);
    int_res = { [.,.,i] -> gaussBlur25(int_img[.,.,i]) };
    return { iv -> newColor(int_res[iv]) | iv < shp };
}
