module Tier4;
use Structures: all;
use Tier3: { relax };
export all;

inline
int[2:shp] sobelEdges(int[2:shp] arr)
{
    weightsY = [
        [ 1,  2,  1],
        [ 0,  0,  0],
        [-1, -2, -1]
    ];
    weightsX = transpose(weightsY);

    resX = relax(weightsX, arr);
    resY = relax(weightsY, arr);
    return min(resX + resY, 255);
}

inline
color[2:shp] sobelEdges(color[2:shp] img)
{
    // Transform to put color on the first axis
    color_major = { [col, y, x] -> toi(img)[y, x, col] };

    blur = { [i] -> sobelEdges(color_major[i]) };

    // Put color back at the last axis
    idx_major = { [y, x, col] -> blur[col, y, x] };

    return { iv -> newColor(idx_major[iv]) | iv < shp };
}
